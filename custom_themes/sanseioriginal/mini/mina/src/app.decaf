Navigation = require('libs/navigation.js')
CartStack = require('libs/cart.js')

utils = require('utils.js')
core = require('core.js')
localize = require('localize.js')

restStore = require('restapi/store.js')
restCustomer = require('restapi/customer.js')


# app
App
  project:
    name: 'SanseiOriginal Mina'
    version: '1.4.1'
    creator: [
      'Redyyu'
    ]

  # global
  store: null
  locale: null
  lang: null

  # instances
  nav: new Navigation()
  cart: new CartStack()
  checkout: new CartStack({infinity: true})

  # lifecycle
  onLaunch: (opts)->
    self = @
    console.info 'Launched...'
    console.info '-------------------'
    console.info self.project.name
    console.info self.project.version
    console.info self.project.creator.join(', ')
    console.log opts.query.debug
    if utils.isDefined(opts.query.debug)
      if opts.query.api
        core.config.baseURL.api = opts.query.api
        console.info 'Debug Mode'
        console.info 'APIUrl is ' + core.config.baseURL.api


    restStore.store.get()
    .then (store)->
      self.store = store
      self.locale = store.locale
      self.lang = store.lang
    .catch ->
      # set locale to '' for transaltor set a empty locale.
      self.locale = core.config.default_locale
    .finally ->
      # inject locale for all pages already launched
      for page in getCurrentPages()
        page.setData
          locale: self.locale

  onShow: (opts)->
    self = @
    self.login()

  onPageNotFound: (opts)->
    app.nav.redirect
      route: core.config.paths.error

  # methods
  set_tabs: ->
    self = @
    if self.tabs_interval_id
      clearInterval(self.tabs_interval_id)
    self.tabs_interval_id = setInterval ->
      return if not self.lang
      tabs = localize.tabs(self.lang)
      for tab, idx in tabs
        wx.setTabBarItem
          index: idx
          text: tab
      clearInterval(self.tabs_interval_id)
    , 100

  set_navbar: (title, is_static)->
    self = @
    if self.navbar_interval_id
      clearInterval(self.navbar_interval_id)
    self.navbar_interval_id = setInterval ->
      return if not self.store
      site_meta = self.store.site_meta or {}
      default_title = site_meta.title or site_meta.slug

      if is_static
        title = localize.title(self.lang, title)

      wx.setNavigationBarTitle
        title: title or default_title

      clearInterval(self.navbar_interval_id)
    , 100

  login_status: false
  login: (callback, force)->
    self = @
    if not utils.isFunction(callback)
      callback = ->
    if core.session.get('token') and not force
      # logged
      callback()
      return
    else if self.login_status
      # other process is try to login
      _loop = 60
      interval_id = setInterval ->
        if not self.login_status and core.session.get('token')
          clearInterval(interval_id)
          callback()
        else if _loop < 0
          clearInterval(interval_id)
        else
          _loop -= 1
      , 600
    else
      # try to login
      _login = (callback, retry)->
        retry = if not utils.isNumber(retry) then 0 else retry + 1
        if retry >= core.config.retry
          wx.redirectTo
            url: core.config.paths.error
          return
        else if retry > 0
          console.info 'retry:', retry
        self.login_status = true

        wx.login
          success: (data) ->
            # get session_key and serve in back-end
            restCustomer.login
              code: data.code
            .then (auth)->
              self.login_status = false
              core.session.set('token', auth.token, auth.expires_in - 60)
              callback()
            .catch (error)->
              self.login_status = false
              _login(callback, retry)
          fail: ->
            self.login_status = false
            _login(callback, retry)
      _login(callback)


  call_service: (opts)->
    self = @
    opts = {} if not opts
    try
      tel = self.store.site_meta.tel
    catch
      tel = null

    if tel
      wx.makePhoneCall
        phoneNumber: tel


  share: (opts)->
    self = @
    if not opts
      try
        site_meta = self.store.site_meta
        opts =
          title: site_meta.title or site_meta.slug
          src: null
          path: null
      catch e
        opts = {}

    share_opts =
      title: opts.title
      imageUrl: opts.src
      path: opts.path

    return share_opts

  get_profile: (callback)->
    self = @
    if not utils.isFunction(callback)
      callback = ->
    if self.profile
      callback(self.profile)
    else
      restCustomer.member.get()
      .then (profile)->
        self.profile = profile
        callback(profile)

  refresh_profile: (callback)->
    self = @
    if not utils.isFunction(callback)
      callback = ->
    restCustomer.member.get()
    .then (profile)->
      self.profile = profile
      callback(profile)

  set_profile: (profile, callback)->
    self = @
    if not utils.isFunction(callback)
      callback = ->
    restCustomer.member.update
      meta: profile
    .then (profile)->
      self.profile = profile
      callback(profile)

  relaunch: ->
    self = @
    core.session.remove('token')
    self.nav.launch
      route: core.config.paths.index
